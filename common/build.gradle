plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    namespace 'jp.room417.common'
    compileSdk compile_sdk_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        // versionCode 1
        // versionName project.describedVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlinOptions {
        jvmTarget = jvm_target
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    // https://youtrack.jetbrains.com/issue/KT-33248
    // noinspection DifferentStdlibGradleVersion
    implementation libs.kotlin.stdlib
    implementation libs.kotlin.reflect
    implementation libs.kotlinx.coroutines.android

    testImplementation libs.androidx.core.testing
    testImplementation libs.mockito.kotlin
    testImplementation libs.junit.jupiter
    testImplementation libs.assertj.core
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    testRuntimeOnly libs.junit.platform.launcher
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

afterEvaluate {
    publishing {
        publications {
            bundleReleaseAar(MavenPublication) {
                from components.release

                groupId 'jp.room417'
                artifactId 'android-common-library'
                version project.describedVersion
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/417-72KI/android-common-library")
                credentials {
                    username = project.localProperties?.get("gpr.user") ?: project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
                    password = project.localProperties?.get("gpr.key") ?: project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
