plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        // versionCode 1
        // versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation project(path: ':common')
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    // https://youtrack.jetbrains.com/issue/KT-33248
    // noinspection DifferentStdlibGradleVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    api 'io.github.417-72ki:twitter4kt:0.2.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'jp.room417'
            artifactId 'twitter4android'
            version project.describedVersion
            artifact sourceJar
            artifact "${buildDir}/outputs/aar/twitter-release.aar"

            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        if (it.group == null || it.name == null || it.version == null || it.name == "unspecified") return
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        if (it.group == rootProject.name) {
                            def dependency = project(":${it.name}")
                            dependencyNode.appendNode('groupId', dependency.publishing.publications.maven.groupId)
                            dependencyNode.appendNode('artifactId', dependency.publishing.publications.maven.artifactId)
                            dependencyNode.appendNode('version', dependency.publishing.publications.maven.version)
                        } else {
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/417-72KI/android-common-library")
            credentials {
                username = project.localProperties?.get("gpr.user") ?: project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
                password = project.localProperties?.get("gpr.key") ?: project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
